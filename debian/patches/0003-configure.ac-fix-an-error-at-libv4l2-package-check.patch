From: Mauro Carvalho Chehab <mchehab+samsung@kernel.org>
Date: Fri, 15 Feb 2019 04:56:11 -0200
Subject: configure.ac: fix an error at libv4l2 package check

Writing m4 logic is hard, as it is required to properly
mark begin/end of each block.

In the case of the libv4l detection check, there's a missing
[] for PKG_CHECK_MODULES(). This causes it to produce a wrong
code, ending by having 2 errors when the script runs:
	checking for libv4l2.h... yes
	./configure: line 15166: V4L2_CFLAGS: command not found
	./configure: line 15167: V4L2_LIBS: command not found

While here, better ident the blocks and fix the error message
when libv4l2.so is not found.

Signed-off-by: Mauro Carvalho Chehab <mchehab+samsung@kernel.org>
---
 configure.ac | 11 ++++++-----
 1 file changed, 6 insertions(+), 5 deletions(-)

diff --git a/configure.ac b/configure.ac
index da334e7..7e9cac6 100644
--- a/configure.ac
+++ b/configure.ac
@@ -241,18 +241,19 @@ AS_IF([test "x$enable_video" = "xno"],
    AC_CHECK_HEADERS([linux/videodev2.h], [have_v4l2="yes"])
    AC_CHECK_HEADERS([libv4l2.h], [have_libv4l="yes"])
    AS_IF([test "x$have_v4l2" = "xno" && test "x$have_v4l1" = "xno"],
-     [AC_MSG_FAILURE([test for video support failed!
+         [AC_MSG_FAILURE([test for video support failed!
 rebuild your kernel to include video4linux support or
 configure --disable-video to skip building video support.])],
-     [test "x$have_v4l2" = "xno"],
-     [AC_MSG_WARN([v4l2 API not detected, upgrade your kernel!])])],
+         [AS_IF([test "x$have_v4l2" = "xno"],
+               [AC_MSG_WARN([v4l2 API not detected, upgrade your kernel!])])]
+        )],
   [AC_CHECK_HEADERS([vfw.h], [with_video="vfw"],
     [AC_MSG_FAILURE([test for VfW video support failed!
 configure --disable-video to skip building video support.])])])
 
 AS_IF([test "x$have_libv4l" = "xyes"],
-   PKG_CHECK_MODULES([V4L2], [libv4l2], [],
-     [AC_MSG_FAILURE([unable to find libv4l.so])]),
+   [PKG_CHECK_MODULES([V4L2], [libv4l2], [],
+     [AC_MSG_FAILURE([unable to find libv4l2.so])])],
   [AC_MSG_WARN([libv4l not detected. Install it to support more cameras!])])
 
 AM_CONDITIONAL([HAVE_VIDEO], [test "x$enable_video" != "xno"])
